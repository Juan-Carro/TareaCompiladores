//# Gram√°tica del lenguaje delta

comment = r'[/][/].*|[/][*](.|\n)*?[*][/]'

identifier = r'[a-zA-Z]\w*'

program = statement* expression EOF
statement = declaration / assignment / if / while
declaration = 'var' decl_variable (',' decl_variable)* ';'
decl_variable = identifier
assignment = lhs_variable '=' expression ';'
lhs_variable = identifier
if = 'if' expression block ('else' block)?
block = '{' statement* '}'
while = 'while' expression block
expression = multiplicative (('+' / '-') multiplicative)*
multiplicative = unary (('*' / '/' / '%') unary)*
unary = ('+' / '-' / '!')* primary
primary = integer_literal / boolean / parenthesis / rhs_variable
decimal = r'0|[1-9][0-9]*'

//#--- PARTE A ---
binary_literal = r'#b[01]+'
octal_literal = r'#o[0-7]+'
hex_literal = r'#x[0-9a-fA-F]+'
integer_literal = binary_literal / octal_literal / hex_literal / decimal





boolean = 'true' / 'false'
parenthesis = '(' expression ')'
rhs_variable = identifier
